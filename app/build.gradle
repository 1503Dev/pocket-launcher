plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

android {
    def localProperties = new Properties()
    def localPropertiesFile = rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        localProperties.load(new FileInputStream(localPropertiesFile))
    }

    signingConfigs {
        debug {
            storeFile file(localProperties.getProperty('SIGNING_KEY_PATH_OVERRIDE'))
            storePassword localProperties.getProperty('SIGNING_KEY_PASSWORD')
            keyPassword localProperties.getProperty('SIGNING_KEY_ALIAS_PASSWORD')
            keyAlias localProperties.getProperty('SIGNING_KEY_ALIAS')
        }
        release {
            storeFile file(localProperties.getProperty('SIGNING_KEY_PATH_OVERRIDE'))
            storePassword localProperties.getProperty('SIGNING_KEY_PASSWORD')
            keyPassword localProperties.getProperty('SIGNING_KEY_ALIAS_PASSWORD')
            keyAlias localProperties.getProperty('SIGNING_KEY_ALIAS')
        }
    }

    namespace 'dev1503.pocketlauncher'
    compileSdk 36

    defaultConfig {
        applicationId "dev1503.pocketlauncher"
        minSdk 26
        //noinspection OldTargetApi
        targetSdk 35
        versionCode 1
        versionName new Date().format('yyyyMMddHHmm') + ".$versionCode"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    aaptOptions {
        additionalParameters "--package-id", "0x40"
        additionalParameters "--allow-reserved-package-id"
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def outputFileName = "${project.name}-v${variant.versionName}-${variant.name}.apk"
            it.outputFileName = outputFileName
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.core
    implementation libs.constraintlayout
    implementation libs.core.ktx
    implementation libs.material3
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.github.L-JINBIN:MTDataFilesProvider:v1.0.0'
    implementation("com.squareup.okio:okio:3.16.2")
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    // Source: https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    compileOnlyApi files("libs/bridge.jar")
    compileOnlyApi files("libs/mc_firebase_1.20.15.jar")

    implementation (project(':minecraftpe'))
}

def kotlinTaskName = "compileDebugKotlin"

tasks.register('buildJavaModApi', Jar) {
    destinationDirectory = file("${project.rootDir}/build/")
    outputs.upToDateWhen { false } // 强制每次都执行

    dependsOn kotlinTaskName

    doFirst {
        def variant = android.applicationVariants.first()
        archiveFileName = "javaModApi-v${variant.versionName}.jar"

        def kotlinDir = file("$buildDir/tmp/kotlin-classes/${variant.name}")
        def javaDir = variant.javaCompileProvider.get().destinationDir

        from kotlinDir.exists() ? [kotlinDir, javaDir] : javaDir

        include 'dev1503/pocketlauncher/mod/**'
        include 'dev1503/pocketlauncher/mod/events/**'
        include 'dev1503/pocketlauncher/modloader/ModInfo.class'
    }
}

tasks.configureEach { task ->
    if (task.name.startsWith('assemble')) {
        task.dependsOn buildJavaModApi
    }
}

// 清理相关任务依赖
clean.doLast {
    delete "${project.rootDir}/build/javaModApi-*.jar"
}