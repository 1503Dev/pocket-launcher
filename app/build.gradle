plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

def RELEASE = 0
def BETA = 1
def SNAPSHOT = 2

def _versionCode = 1
def _versionName = "1.0.0"
def _variant = BETA

android {
    def localProperties = new Properties()
    def localPropertiesFile = rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        localProperties.load(new FileInputStream(localPropertiesFile))
    }

    if (localProperties.containsKey("BUILD_IN_CI") && localProperties.getProperty("BUILD_IN_CI") == "true") {
        _variant = SNAPSHOT
    }

    signingConfigs {
        debug {
            storeFile file(localProperties.getProperty('SIGNING_KEY_PATH_OVERRIDE'))
            storePassword localProperties.getProperty('SIGNING_KEY_PASSWORD')
            keyPassword localProperties.getProperty('SIGNING_KEY_ALIAS_PASSWORD')
            keyAlias localProperties.getProperty('SIGNING_KEY_ALIAS')
        }
        release {
            storeFile file(localProperties.getProperty('SIGNING_KEY_PATH_OVERRIDE'))
            storePassword localProperties.getProperty('SIGNING_KEY_PASSWORD')
            keyPassword localProperties.getProperty('SIGNING_KEY_ALIAS_PASSWORD')
            keyAlias localProperties.getProperty('SIGNING_KEY_ALIAS')
        }
    }

    namespace 'dev1503.pocketlauncher'
    compileSdk 36

    defaultConfig {
        def buildTime = new Date().format('yyyyMMddHHmm')
        def variantSuffix = _variant == RELEASE ? "release" :
                _variant == BETA ? "beta" : "SNAPSHOT"

        applicationId "dev1503.pocketlauncher"
        minSdk 26
        //noinspection OldTargetApi
        targetSdk 35
        versionCode _versionCode
        versionName "${_versionName}.${_versionCode}.${buildTime}-${variantSuffix}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    aaptOptions {
        additionalParameters "--package-id", "0x05"
        additionalParameters "--allow-reserved-package-id"
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def outputFileName = "PocketLauncher-v${variant.versionName}.apk"
            it.outputFileName = outputFileName
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.core
    implementation libs.constraintlayout
    implementation libs.core.ktx
    implementation libs.material3
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.github.L-JINBIN:MTDataFilesProvider:v1.0.0'
    implementation("com.squareup.okio:okio:3.16.2")
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    // Source: https://mvnrepository.com/artifact/net.raphimc/MinecraftAuth
    implementation 'net.raphimc:MinecraftAuth:5.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'com.github.li-xiaojun:XPopup:2.10.0'

    debugImplementation("com.reqable.android:user-certificate-trust:1.0.0")

    compileOnlyApi fileTree(dir: '../api', include: ['*.jar'])

    implementation (project(':minecraftpe'))
    implementation project(':common')
}

def kotlinTaskName = "compileDebugKotlin"

tasks.register('buildJavaModApi', Jar) {
    destinationDirectory = file("${project.rootDir}/build/")
    outputs.upToDateWhen { false }

    dependsOn kotlinTaskName
    dependsOn ':minecraftpe:assemble'
    dependsOn ':common:assemble'

    doFirst {
        def variant = android.applicationVariants.first()
        archiveFileName = "javaModApi-v${variant.versionName}.jar"

        def mainKotlinDir = file("$buildDir/tmp/kotlin-classes/${variant.name}")
        File mainJavaDir = variant.javaCompileProvider.get().destinationDir

        def minecraftpeJavaDir = file(mainJavaDir.path.replace('app', 'minecraftpe'))
        def commonJavaDir = file(mainJavaDir.path.replace('app', 'common'))
        def commonKotlinDir = file(mainKotlinDir.path.replace('app', 'common'))

//        println("mainKotlinDir: $mainKotlinDir")
//        println("mainJavaDir: $mainJavaDir")
//        println("minecraftpeJavaDir: $minecraftpeJavaDir")
//        println("commonJavaDir: $commonJavaDir")
//        println("commonKotlinDir: $commonKotlinDir")

        from mainKotlinDir.exists() ? [mainKotlinDir, mainJavaDir] : mainJavaDir
        from minecraftpeJavaDir.exists() ? minecraftpeJavaDir : []
        from commonKotlinDir.exists() ? [commonJavaDir, commonKotlinDir] : []

        include 'dev1503/pocketlauncher/mod/**'
        include 'dev1503/pocketlauncher/mod/types/**'
        include 'dev1503/pocketlauncher/mod/events/**'
        include 'dev1503/pocketlauncher/modloader/ModInfo.class'
        include 'dev1503/pocketlauncher/Log.class'
        include 'dev1503/pocketlauncher/KVConfig.class'
        include 'dev1503/pocketlauncher/Utils.class'
        include 'dev1503/pocketlauncher/InstanceInfo.class'
    }
}

tasks.configureEach { task ->
    if (task.name.startsWith('assemble')) {
        task.dependsOn buildJavaModApi
    }
}

clean.doLast {
    delete "${project.rootDir}/build/javaModApi-*.jar"
}